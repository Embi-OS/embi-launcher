function(target_try_link_libraries TARGET LIBRARY PREFIX)

    find_package(PkgConfig QUIET)
    if(PKG_CONFIG_FOUND)
        pkg_check_modules(${PREFIX} QUIET ${LIBRARY})
    endif()

    if(${PREFIX}_FOUND)
        message(NOTICE "${LIBRARY} version ${${PREFIX}_VERSION} found. Building with ${PREFIX} support.")
        target_link_libraries(${TARGET} PRIVATE ${${PREFIX}_LIBRARIES})
        target_compile_definitions(${TARGET} PRIVATE ${PREFIX}_FOUND)
    else()
        find_library(${PREFIX}_LIB NAMES ${LIBRARY})
        if(${PREFIX}_LIB)
            message(NOTICE "${LIBRARY} found. Building with ${PREFIX} support.")
            target_link_libraries(${TARGET} PRIVATE ${${PREFIX}_LIB})
            target_compile_definitions(${TARGET} PRIVATE ${PREFIX}_FOUND)
        else()
            message(NOTICE "${LIBRARY} not found. Building ${TARGET} without ${PREFIX} support.")
        endif()
    endif()

endfunction()
